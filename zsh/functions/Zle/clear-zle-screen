#!/bin/zsh

## Clear the screen after CLRTMOUT seconds of inactivity
## Peter Stephenson, Angel Olivera, Tue 12.Jun.07
## <200706121514.l5CFEaH5029180@news01.csr.com>

zmodload -i zsh/stat
zmodload -i zsh/datetime

clear-zle-screen() {
    local -a mtime
    integer diff

    stat -A mtime +mtime $TTY
    (( diff = EPOCHSECONDS - ${mtime} ))
    if (( diff >= CLRTMOUT )); then
	zle && zle clear-screen-bottom
    # function called from precmd(), to avoid multiple clearouts
    #    sched +$CLRTMOUT clear-zle-screen
    else
	sched +$(( CLRTMOUT - diff )) clear-zle-screen
    fi
}

functions[precmd]+='
sched | 
	while read line; do 
		if [[ $line == *clear-zle-screen* ]]; then 
			sched -${line%% *}
		fi
	done
sched +$CLRTMOUT clear-zle-screen
'
clear-zle-screen
