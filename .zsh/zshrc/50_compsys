#
# Completion
#

zmodload -i zsh/complist
autoload -Uz compinit
compinit -C

## completers order
# run rehash before completing
autoload -U _force_rehash
# complete files when hitting tab on an empty line
autoload -U _listfiles
zstyle ':completion:*' completer _listfiles _force_rehash _expand _complete _prefix _correct _approximate

## add a space after word completed by _prefix
zstyle ':completion:*:prefix:*'		add-space true

## verbose descriptions
zstyle ':completion:*'			verbose yes

## list-colors
zstyle ':completion:*'			list-colors ${(s.:.)LS_COLORS}

## 'format' style
zstyle ':completion:*:descriptions'	format "- %d -"
zstyle ':completion:*:messages'		format "- %d -"
zstyle ':completion:*:corrections'	format "- %d (errors: %e) -"
zstyle ':completion:*:warnings'		format "- no matches: %d -"

## options with no descriptions will display the description of their arguments
zstyle ':completion:*'			auto-description "argument: %d"

## group-name
## the empty string will make zsh use tag names for the group name
zstyle ':completion:*'			group-name ''

## sections
# man pages
zstyle ':completion:*:manuals'		separate-sections true
zstyle ':completion:*:manuals.(^1*)'	insert-sections   true
# dict entries
zstyle ':completion:*:words'		separate-sections true

## list-separator
zstyle ':completion:*'			list-separator '#'

## tolerate errors
zstyle -e ':completion:*:approximate:*'	max-errors 'reply=( $(( ($#PREFIX + $#SUFFIX) / 3 )) )'
zstyle -e ':completion:*:correct:*'	max-errors 'reply=( $(( ($#PREFIX + $#SUFFIX) / 2 )) )'

## Always use menu completion
# select=<N>
zstyle ':completion:*'			menu select

## when pasting, do not interpret <tab> if there are more characters queued for
## reading
zstyle ':completion:*'			insert-tab pending

## $hosts completion
typeset -U hosts

# known_hosts
[[ -e ~/.ssh/known_hosts ]] && hosts=(${${${(f)"$(<$HOME/.ssh/known_hosts)"}%%\ *}%%,*})		# including IP addresses
# hosts=(${${${${(f)"$(<$HOME/.ssh/known_hosts)"}:#[0-9]*}%%\ *}%%,*})	# excluding IP addresses

# $REMOTEHOST || $SSH_CLIENT
hosts+=(${REMOTEHOST:-${SSH_CLIENT%% *}})

zstyle ':completion:*:hosts'			hosts $hosts
zstyle ':completion:*:complete:ssh:*:hosts'	hosts $hosts
zstyle ':completion:*:(nc|ping|ssh|nmap|*ftp|telnet|finger):*'	hosts $hosts

## don't complete completion functions (those starting with _)
zstyle ':completion::complete:*:functions'		ignored-patterns '_*'

## ignored-patterns
zstyle ':completion:*:*:(^rm):*:*files'			ignored-patterns '*?.o' '*?\~'
zstyle ':completion::*:(vi|vim):*'                      ignored-patterns '*?.(lo|so|la|o)'

## don't consider words already on the command line for completion
zstyle ':completion:*:(rm|kill):*'                      ignore-line yes

## when completing parameters, prefer expanding exact matches over completing
## longer parameter names, but still offer them
zstyle ':completion:*:expand:*'				accept-exact continue

## ignore prefix for _approximate
## this helps e.g. when completing options: bzip2 _-foo<TAB>
zstyle ':completion::approximate*:*'			prefix-needed false

## processes completion (e.g. kill)
# if sorted, then --forest becomes useless
zstyle ':completion:*:*:kill:*:processes'               sort false
zstyle ':completion:*:processes'                        command 'ps --forest -A -o pid,user,cmd'
zstyle ':completion:*:processes-names'                  command 'ps c -u ${USER} -o command | uniq'
zstyle ':completion:*:processes'			command 'ps --forest -au$USER -o pid,time,cmd | grep -v "ps .* pid,time,cmd"'
zstyle ':completion:*:*:kill:*:processes'		list-colors '=(#b) #([0-9]#)[ 0-9:]#([^ ]#)*=01;30=01;31=01;38'


if [[ $HOSTNAME == refinery* ]]; then
    zstyle ':completion:*:urls' local aolivera.com.ar /var/www/aolivera.com.ar/htdocs
    zstyle ':completion:*:urls' local twat.com.ar ~/public_html
fi

### Credits:
## Kiddle, Peek, & Stephenson. (2005). "From Bash to Z Shell". Apress.
## file:///usr/share/doc/zsh/examples/*
## http://zshwiki.org/home/examples/compquickstart
## http://ft.bewatermyfriend.org/
## http://madism.org/~madcoder/dotfiles/zsh/40_completion

# vim:ft=zsh
