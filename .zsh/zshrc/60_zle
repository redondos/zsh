#
# Zle
#

## Emacs mode
bindkey -e

## Navigation
[[ -n ${key[Home]}      ]]  &&  bindkey "${key[Home]}"      beginning-of-line
[[ -n ${key[Insert]}    ]]  &&  bindkey "${key[Insert]}"    overwrite-mode
[[ -n ${key[Delete]}    ]]  &&  bindkey "${key[Delete]}"    delete-char
[[ -n ${key[End]}       ]]  &&  bindkey "${key[End]}"       end-of-line
[[ -n ${key[PageUp]}    ]]  &&  bindkey "${key[PageUp]}"    history-search-backward
[[ -n ${key[PageDown]}  ]]  &&  bindkey "${key[PageDown]}"  history-search-forward
[[ -n ${key[Up]}        ]]  &&  bindkey "${key[Up]}"        up-line-or-history
[[ -n ${key[Down]}      ]]  &&  bindkey "${key[Down]}"      down-line-or-history
[[ -n ${key[Left]}      ]]  &&  bindkey "${key[Left]}"      backward-char
[[ -n ${key[Right]}     ]]  &&  bindkey "${key[Right]}"     forward-char

## listscroll
# bindings for the list-prompt style
[[ -n ${key[Up]}        ]]  &&  bindkey -M listscroll "${key[Up]}"        up-line-or-history
[[ -n ${key[Down]}      ]]  &&  bindkey -M listscroll "${key[Down]}"      down-line-or-history

## menuselect
bindkey -M menuselect '^[h' backward-char
bindkey -M menuselect '^[l' forward-char
bindkey -M menuselect '^[j' down-line-or-history
bindkey -M menuselect '^[k' up-line-or-history

## Kill that word
bindkey '^\\' backward-kill-word

## Turn prediction on/off
if autoload predict-on ; then
    zle -N predict-on
    zle -N predict-off
    bindkey '^X^T^N' predict-on
    bindkey '^X^T^F' predict-off
fi

## ctrl-x, f inserts the latest modified file the the command line
bindkey -s '^xf' '*(.om[1])\t'

## expand-or-complete-with-dots
## Ian Langworth, 13.Jun.07 17:53
## <b6c719b90706131353l3c12764eg893b3ff39294dcde@mail.gmail.com>
# autoload expand-or-complete-with-dots
# zle -N expand-or-complete-with-dots
# bindkey "^I" expand-or-complete-with-dots

# Also show what completer is being tried.
zstyle ":completion:*" show-completer true

## Redo
bindkey '^xr' 'redo'
bindkey '^x^r' 'redo'

## Search in history for lines that are exactly the same from the start up to
## the cursor position.
bindkey '^xp' history-beginning-search-backward
bindkey '^xn' history-beginning-search-forward

## vi-${direction}-word
# bindkey '\eB' vi-backward-word
# bindkey '\eF' vi-forward-word

## Better handling of multi-line commands than push-line, especially in the
## buffer stack
bindkey '\eq' push-line-or-edit

## Enter directories in menu selection, from zshguide.
bindkey -M menuselect '^o' accept-and-infer-next-history

## Easier to navigate menus
accept-and-execute-line() { zle accept-line ; zle accept-line }
zle -N accept-and-execute-line
# bindkey -M menuselect '^M' accept-and-execute-line

## Completion help
bindkey '^xh' _complete_help

## Manual completion calls
bindkey '^X^R' _read_comp

## delete-word-alnum
delete-word-alnum() { 
    typeset WORDCHARS=
    zle delete-word
}
zle -N delete-word-alnum
# bindkey "\ed" delete-word-alnum

## copy-prev-word
## default is \e^_ (ctrl+alt+shift+-)
bindkey '\e=' copy-prev-word

## zsticky
## Bart Schaefer, <080113000048.ZM15017@torch.brasslantern.com>
autoload -U zsticky

## Fast directory navigation
# http://blog.orebokech.com/2008/03/fast-directory-navigation.html
function up-one-dir { set -E; pushd ..; set +E; zle redisplay; zle -M `pwd` }
function back-one-dir { set -E; popd; set +E; zle redisplay; zle -M `pwd` }
zle -N up-one-dir
zle -N back-one-dir
bindkey "^[-" up-one-dir
bindkey "^[=" back-one-dir

## expand-dot
# http://rusmafia.org/linux/zsh-parent-dir-expand
# it messes up incremental completion
# autoload -U expand-dot
# zle -N expand-dot
# bindkey . expand-dot

### some scripts from either of these three (who copied from who?)
# http://wael.nasreddine.com/cgi-bin/viewvc.cgi/wael/trunk/etc/.zsh/zle?revision=628&view=markup&sortby=author
# http://hg.codemac.net/config/
# git://github.com/gigamo/config.git
## bracket_automatcher
bracket_automatcher() {
    zle self-insert
    zle vi-backward-char
    zle vi-match-bracket
    zle -R
    sleep 0.2
    zle vi-match-bracket
    zle forward-char
}
zle -N bracket_automatcher
bindkey ']' bracket_automatcher
bindkey '}' bracket_automatcher
bindkey ')' bracket_automatcher
#
## clear-bottom
clear-screen-bottom () {
    echo -n "\033[2J\033[400H"
    builtin zle .redisplay
}
zle -N clear-screen-bottom
bindkey '^L' clear-screen-bottom
bindkey '\e^L' clear-screen-bottom

## expand-or-complete-prefix is much cooler
bindkey '^I' expand-or-complete-prefix

## Cycle in reverse with meta-tab
bindkey '\e^I' reverse-menu-complete

## ctrl-o to browse directory hierarchies
bindkey  -M  menuselect  '^o'  accept-and-infer-next-history

## shift-[up/down] jumps in history even with multiline commands
bindkey '\e[a' up-history
bindkey '\e[b' down

## load command line in $EDITOR
autoload -U edit-command-line
zle -N edit-command-line
bindkey '\ee' edit-command-line

## alt-/ completes words from older commands
bindkey '\e/' _history-complete-older

# vim:ft=zsh
