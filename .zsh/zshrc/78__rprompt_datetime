#
# Alternating right prompt
#

# Alternate between these expressions and append to the end of RPROMPT
__rprompt_datetime_items=('%D{%T}' ' %D{%b %d} ')
__rprompt_datetime_items=("$__rprompt_datetime_items[1]" "$__rprompt_datetime_items[@]")
# Color codes appended to the array by using ^ (RC_EXPAND_PARAM)
__rprompt_datetime_items=(\[%{${fg[yellow]}%}${^__rprompt_datetime_items}%{${fg[default]}%}\])
# Path
# this is too damn long!
# RPROMPT_PATH="%{${fg[cyan]}%}%/%{${fg[default]}%} "

__rprompt_datetime() {
    if ((__rprompt_datetime)); then
        for ((i=2; i<= ${#__rprompt_datetime_items}; i++)); do
            if [[ $__rprompt_datetime_items[i] == $__rprompt_datetime_items[1] ]]; then
                if [[ -n $__rprompt_datetime_items[((i+1))] ]]; then
                    __rprompt_datetime_items[1]=$__rprompt_datetime_items[((i+1))]
                else
                    __rprompt_datetime_items[1]=$__rprompt_datetime_items[2]
                fi
                break
            fi
        done

        RPROMPT+="${__rprompt_datetime_items[1]}"
    fi
}

precmd_functions+=(__rprompt_datetime)

# vim: ft=zsh:ts=4:sw=4:et
